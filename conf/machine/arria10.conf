#@TYPE: Machine
#@NAME: arria10
#@DESCRIPTION: Machine configuration for the Arria 10 SoC" 

require conf/machine/include/socfpga.inc

SPL_BINARY = "spl/u-boot-splx4.sfp"
UBOOT_BINARY = "u-boot.img"

UBOOT_CONFIG ??= "arria10-socdk"

KMACHINE = "arria10"

EXTRA_IMAGEDEPENDS_append = " \
	${@bb.utils.contains("IMAGE_TYPE", "pr", " u-boot-socfpga-scr", "", d)} \
	${@bb.utils.contains("IMAGE_TYPE", "pr", "", " hw-ref-design", d)} \
	"

# Default kernel devicetrees
KERNEL_DEVICETREE ?= "\
			socfpga_arria10_socdk_sdmmc.dtb \
			socfpga_arria10_socdk_qspi.dtb \
			socfpga_arria10_socdk_nand.dtb \
			${@bb.utils.contains("IMAGE_TYPE", "pr", "fpga_region.dtb", "", d)} \
			${@bb.utils.contains("IMAGE_TYPE", "pr", "persona0.dtb", "", d)} \
			${@bb.utils.contains("IMAGE_TYPE", "pr", "persona1.dtb", "", d)} \
			"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_append = "\
					  ${@bb.utils.contains("IMAGE_TYPE", "pr", " kernel-devicetree", "", d)} \
					  ${@bb.utils.contains("IMAGE_TYPE", "pr", " hw-ref-design", "", d)} \
					  "

EXTRA_IMAGECMD_jffs2 ?= "-n -q \
	${@bb.utils.contains("IMAGE_TYPE", "nand", "-e 0x20000 -s 0x200", "", d)} \
	${@bb.utils.contains("IMAGE_TYPE", "qspi", "-e 0x10000", "", d)} \
	"

SERIAL_CONSOLES ?= "115200;ttyS0"

UBOOT_EXTLINUX ?= "1"
UBOOT_EXTLINUX_LABELS ?= "default"
UBOOT_EXTLINUX_DEFAULT_LABEL ?= "Arria10 SOCDK SDMMC"

UBOOT_EXTLINUX_MENU_DESCRIPTION_default ?= "Arria10 SOCDK SDMMC"
UBOOT_EXTLINUX_KERNEL_IMAGE_default ?= "../${KERNEL_IMAGETYPE}"
UBOOT_EXTLINUX_FDTDIR_default ?= ".."
UBOOT_EXTLINUX_CONSOLE ?= "console=ttyS0,115200n8"
UBOOT_EXTLINUX_KERNEL_ARGS_default ?= "rootwait rw earlyprintk memmap=16M$0x3F000000"
UBOOT_EXTLINUX_ROOT_default ?= "root=/dev/mmcblk0p2"

IMAGE_BOOT_FILES ?= " \
	${@bb.utils.contains("IMAGE_TYPE", "", "", "socfpga_arria10_socdk_sdmmc.dtb", d)} \
	${@bb.utils.contains("IMAGE_TYPE", "pcie", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
	${@bb.utils.contains("IMAGE_TYPE", "sgmii", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
	${@bb.utils.contains("IMAGE_TYPE", "tse", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
	${@bb.utils.contains("IMAGE_TYPE", "pr", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
	fit_spl_fpga_${IMAGE_TYPE}.itb;fit_spl_fpga.itb \
        ${KERNEL_IMAGETYPE} \
        ${UBOOT_BINARY} \
        extlinux.conf;extlinux/extlinux.conf \
	${@bb.utils.contains("IMAGE_TYPE", "pr", "boot.scr", "", d)} \
        "

WKS_FILE ?= "sdimage-arria10.wks"
IMAGE_FSTYPES +="wic"
